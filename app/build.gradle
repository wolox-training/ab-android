apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'idea'
apply plugin: 'jacoco-android'
apply from: 'keystore.gradle'
apply from: '../jacoco.gradle'
apply from: '../quality.gradle'
apply from: '../scripts/warp/warp_task.gradle'
apply from: '../scripts/sanity/sanity_task.gradle'

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    defaultConfig {
        applicationId "ar.com.wolox.android.example"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 1
        versionName "1.0.0"

        setProperty("archivesBaseName", "WoloxApp-$versionName") // Generated APK file name
    }

    signingConfigs {
        debug {
            storeFile file('keystore/debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
        release {
            storeFile release_keystore
            storePassword store_password
            keyAlias key_alias
            keyPassword key_password
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            signingConfig signingConfigs.debug
            testCoverageEnabled = true
        }
    }

    // Change the "default" to whatever fits. At least one 'dimension' is required by gradle.
    flavorDimensions "default"

    productFlavors {
        stage {
            applicationId defaultConfig.applicationId + ".stage"
            versionName defaultConfig.versionName + "-stage"
            resValue "string", "app_name", "[Stage] WoloxApp"
        }

        production {
            resValue "string", "app_name", "WoloxApp"
        }
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

jacocoAndroidUnitTestReport {
    excludes = defaultExcludes + ['**/*_Factory.class']
}

buildscript {
    ext.support_library_version = '28.0.0'
    ext.dagger_version = '2.13'
    ext.wolmo_version = '2.0.5'
    ext.butterknife_version = '8.8.1'
    ext.leak_canary_version = '1.5.4'
    ext.chuck_version = '1.1.0'
    ext.powermock_version = '1.7.0'
    ext.fresco_version = '1.12.1'
    ext.recyclerview_version = '28.0.0'
    ext.constraint_version = '1.1.3'
    ext.retrofit2_version = '2.5.0'
    ext.powermock_version = '1.7.0'
    ext.support_version = '28.0.0'
    ext.squareup_version = '3.9.0'
    ext.junit_version = '4.12'
    ext.mockito_version = '2.13.0'
    ext.assertj_version = '3.9.0'
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    //Fresco
    implementation "com.facebook.fresco:fresco:$fresco_version"

    //RecyclerView
    implementation "com.android.support:recyclerview-v7:$recyclerview_version"

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofit2_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit2_version"

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // prettytime
    implementation "org.grails.plugins:grails-pretty-time:4.0.0"

    // Wolmo
    implementation "com.github.wolox:wolmo-networking-android:$wolmo_version"

    // Android support libraries
    implementation "com.android.support:support-compat:$support_library_version"
    implementation "com.android.support:recyclerview-v7:$support_library_version"
    implementation "com.android.support:design:$support_library_version"

    // Dagger
    implementation "com.google.dagger:dagger:$dagger_version"
    implementation "com.google.dagger:dagger-android-support:$dagger_version"
    implementation "com.android.support.constraint:constraint-layout:$constraint_version"
    implementation "com.android.support:support-v4:$support_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"
    kapt "com.google.dagger:dagger-android-processor:$dagger_version"

    // Third-party
    implementation "com.jakewharton:butterknife:$butterknife_version"
    kapt "com.jakewharton:butterknife-compiler:$butterknife_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$squareup_version"
    debugImplementation "com.readystatesoftware.chuck:library:$chuck_version"
    releaseImplementation "com.readystatesoftware.chuck:library-no-op:$chuck_version"
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leak_canary_version"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leak_canary_version"

    // Testing dependencies
    testImplementation "junit:junit:$junit_version"
    testImplementation "org.mockito:mockito-inline:$mockito_version"
    testImplementation "org.assertj:assertj-core:$assertj_version"
    testImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leak_canary_version"
}
